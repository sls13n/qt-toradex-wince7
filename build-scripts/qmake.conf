# qmake configuration for Windows CE 7.0 (Toradex_CE700) with VS2008 on ARM targets
#
# Custom mkspec for Toradex Windows CE 7.0 ARM devices

include(../common/wince/qmake.conf)

CE_SDK                  = Toradex_CE700
CE_ARCH                 = armv4i

DEFINES                += NOMINMAX QT_NO_CLIPBOARD QT_NO_ACCESSIBILITY QT_NO_NATIVE_GESTURES QT_NOSTANDARDSHELL_UI_MODEL _CRT_SECURE_NO_DEPRECATE _WIN32_WCE=0x700 $$CE_ARCH _ARMV7_ armv7 _ARM_ ARM _M_ARM ARM __arm__

QMAKE_LFLAGS_CONSOLE    = /SUBSYSTEM:WINDOWSCE,7.00 /MACHINE:THUMB /ENTRY:mainACRTStartup
QMAKE_LFLAGS_WINDOWS    = /SUBSYSTEM:WINDOWSCE,7.00 /MACHINE:THUMB
QMAKE_LFLAGS_DLL        = /SUBSYSTEM:WINDOWSCE,7.00 /MACHINE:THUMB /DLL /SAFESEH:NO
QMAKE_LIBFLAGS_RELEASE  = /LTCG
QMAKE_LIBS              = corelibc.lib coredll.lib
QMAKE_LIBS_CORE         = corelibc.lib ole32.lib oleaut32.lib uuid.lib commctrl.lib coredll.lib winsock.lib
QMAKE_LIBS_GUI          = ceshell.lib ole32.lib $$QMAKE_LIBS_CORE
QMAKE_LIBS_NETWORK      = ws2.lib $$QMAKE_LIBS_GUI
QMAKE_LIBS_OPENGL       =
QMAKE_LIBS_COMPAT       =
QMAKE_LIBS_OPENVG       = libopenvg.lib
QMAKE_LIBS_OPENGL_ES2   = libEGL.lib libGLESv2.lib

QMAKE_RC                = rc

QMAKE_COMPILER_DEFINES -= _MSC_VER=1400
QMAKE_COMPILER_DEFINES += _MSC_VER=1500

# Toradex SDK paths
# Use short (8.3) DOS paths to avoid issues with spaces in qmake-generated Makefiles
# CE_SDK_DIR = $$(CE_SDK_DIR)
# isEmpty(CE_SDK_DIR):CE_SDK_DIR = C:/PROGRA~2/WI84B9~1/SDKs/$$CE_SDK

# QMAKE_INCDIR           += $$CE_SDK_DIR/Include/Armv4i
# QMAKE_LIBDIR           += $$CE_SDK_DIR/Lib/Armv4i

# Override runtime library to use static linking for WinCE (must be after include)
# Must override both base flags and release/debug variants to ensure -MT is used
QMAKE_CFLAGS           += -MT
QMAKE_CFLAGS_RELEASE    = -O2 -MT
QMAKE_CFLAGS_DEBUG      = -Zi -MTd
QMAKE_CXXFLAGS         += -MT
QMAKE_CXXFLAGS_RELEASE  = -O2 -MT
QMAKE_CXXFLAGS_DEBUG    = -Zi -MTd
# Override exception handling to work with static runtime
QMAKE_CXXFLAGS_EXCEPTIONS_ON  = -EHsc -MT
QMAKE_CXXFLAGS_EXCEPTIONS_OFF = -EHs-c- -MT